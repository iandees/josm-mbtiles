plugins{
  id 'org.openstreetmap.josm' version '0.8.2'
}
apply plugin: 'eclipse'
apply plugin: 'java'

apply from: 'gradle/version-functions.gradle'

sourceCompatibility = '1.8'
version = getVersionName()

repositories {
  mavenCentral()
}

dependencies {
  packIntoJar 'org.xerial:sqlite-jdbc:3.49.1.0'
  testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
}

test {
  useJUnitPlatform()
}

sourceSets {
  main {
    java {
      srcDirs = ['src']
      exclude 'test/**'
    }
    resources {
      srcDirs = ["$projectDir"]
      include 'data/**'
      include 'images/**'
      include 'README.md'
    }
  }
  test {
    java {
      srcDirs = ['src/test/java']
    }
  }
}

base {
  archivesName = 'mbtiles'
}
josm {
  debugPort = 4139
  manifest {
    oldVersionDownloadLink 14632, "v2.5.0", new URL("https://github.com/iandees/josm-mbtiles/releases/download/v2.5.0/mbtiles.jar")
    oldVersionDownloadLink 13733, "v2.4.0", new URL("https://github.com/iandees/josm-mbtiles/releases/download/v2.4.0/mbtiles.jar")
    oldVersionDownloadLink 12677, "v2.3.3", new URL("https://github.com/iandees/josm-mbtiles/releases/download/v2.3.3/mbtiles.jar")
  }
}

eclipse.project.name = 'JOSM-mbtiles'
eclipseClasspath.dependsOn cleanEclipseClasspath
eclipseProject.dependsOn cleanEclipseProject

tasks.withType(Javadoc) {
  failOnError false
}
